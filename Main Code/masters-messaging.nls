; Process the inbox message by message. Only messages sent between
; last_read (incusive) and ticks (exclusive) are considered. This ensures
; consistency in concurrent message processing.
; Sets:
;   {number} last_read:
to master-process-inbox
  let received_messages filter [item 0 ? >= last_read and item 0 ? < ticks] inbox

  foreach received_messages [
    let received_message ?
    let message_tick item 0 received_message
    let sender item 1 received_message
    let msg item 2 received_message

    let message_type substring msg 0 position " " msg
    let message_body substring msg (position " " msg + 1) (length msg)

    if message_type = "MASTER_INIT" [
      process-master-init-message message_type message_body sender
    ]
    if message_type = "MASTER_INIT_REPLY" [
      process-master-init-reply-message message_type message_body sender
    ]
    if message_type = "WORKER_INIT" [
        process-worker-init-message message_type message_body sender
    ]
    if message_type = "SOS" [
      process-sos-message message_type message_body sender
    ]
    if message_type = "SOS_REPLY" [
      process-sos-reply message_type message_body sender
    ]
    if message_type = "HELP_ARRIVED" [
        process-help-arrived-message message_type message_body sender
    ]
    if message_type = "PROVIDING_HELP" [
        process-providing-help-message message_type message_body sender
    ]
    if message_type = "VOTE" [
      process-vote-message message_type message_body sender
    ]
  ]

  set last_read ticks
end

; Store the sender's broadcasted bus route on the master_fleet and  send reply.
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} master_fleet
to process-master-init-message [message_type message_body sender_id]
  let message_tokens split-string message_body " "
  let route (item 0 message_tokens)

  table:put master_fleet sender_id route

  send sender_id "MASTER_INIT_REPLY" route_id
end

; Store the sender's replied bus route on the master_fleet.
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} master_fleet
to process-master-init-reply-message [message_type message_body sender_id]
    let message_tokens split-string message_body " "
    let route (item 0 message_tokens)

    table:put master_fleet sender_id read-from-string route
end

; Store the list of initialised workers and respective bus types.
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} worker_inits
to process-worker-init-message [message_type message_body sender_id]
    let message_tokens (split-string message_body " ")
    let worker_type (read-from-string (item 0 message_tokens))
    let current_worker_inits []
    if table:has-key? worker_inits ticks
    [ set current_worker_inits table:get worker_inits ticks ]
    set current_worker_inits lput (list sender_id worker_type) current_worker_inits
    table:put worker_inits ticks current_worker_inits
end

; Store the list of sos-senders by tick.
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} received_sos_messages
to process-sos-message [message_type message_body sender_id]
  let message_tokens (split-string message_body " ")
  let message_details (list sender_id read-from-string (item 0 message_tokens))
  let current_sos_messages []
  ifelse (table:has-key? received_sos_messages ticks)
  [
    set current_sos_messages (table:get received_sos_messages ticks)
    set current_sos_messages (lput message_details current_sos_messages)
  ]
  [
    set current_sos_messages (list message_details)
  ]
  table:put received_sos_messages ticks current_sos_messages
end


; Store the sos replies by tick.
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} outbox
to process-sos-reply [message_type message_body sender_id]
    let outbox_group table:get outbox "SOS"

    let expected_senders table:keys (table:get outbox_group (ticks - 2))
    let current_sos_replies []
    if (table:has-key? sos_replies ticks) [
        set current_sos_replies table:get sos_replies ticks
    ]

    foreach expected_senders [
        let expected_sender ?
        if sender_id = expected_sender [
            let sender_bus_capacity read-from-string message_body
            set current_sos_replies (lput sender_bus_capacity current_sos_replies)
        ]
    ]

    table:put sos_replies ticks current_sos_replies
end

; Store the arrival of a worker by tick.
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} worker_arrived
to process-help-arrived-message [message_type message_body sender_id]
    table:put worker_arrived ticks (list sender_id)
end

; Remove a worker from the current route_fleet since it is hedaing for a new route.
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} route_fleet
to process-providing-help-message [message_type message_body sender_id]
    table:remove route_fleet (read-from-string message_body)
end

; Store all the current votes by tick
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} votes
to process-vote-message [message_type message_body sender_id]
  let message_tokens (split-string message_body " ")
  let message_details (list sender_id (read-from-string (item 0 message_tokens)) (read-from-string (item 1 message_tokens)))

  let current_votes []
  ifelse (table:has-key? votes ticks)
  [
    set current_votes (table:get votes ticks)
    set current_votes (lput message_details current_votes)
  ]
  [
    set current_votes (list message_details)
  ]
  table:put votes ticks current_votes
end

; Process the inbox message by message. Only messages sent between
; last_read (incusive) and ticks (exclusive) are considered. This ensures
; consistency in concurrent message processing.
; Sets:
;   {number} last_read:
to worker-process-inbox
  let received_messages filter [item 0 ? >= last_read and item 0 ? < ticks] inbox
  foreach received_messages [
    let received_message ?
    let message_tick item 0 received_message
    let sender item 1 received_message
    let msg item 2 received_message

    let message_type substring msg 0 position " " msg
    let message_body substring msg (position " " msg + 1) (length msg)

    if message_type = "WORKER_INIT_REPLY" [
      process-worker-init-reply-message message_type message_body sender
    ]
    if message_type = "HELP_REQUEST" [
      process-help-request-message message_type message_body sender
    ]
  ]

  set last_read ticks
end

; Store the reply of a master by tick.
; Sets:
;   {table} worker_init
to process-worker-init-reply-message [message_type message_body sender_id]
    format-log 0 (word "Received WORKER_INIT_REPLY from " sender_id)
    if not table:has-key? worker_init_replies ticks [
        table:put worker_init_replies ticks []
    ]

    let values table:get worker_init_replies ticks
    set values lput (list sender_id message_body) values
    table:put worker_init_replies ticks values
end

; Register that bus will provide help after its current trip is completed.
; In other words, it just adds the help requests in a dedicated list.
; Params:
;   {string} message_type: The type of message.
;   {string} message_body: the content of the message.
;   {number} sender_id: the bus_id of the sender
; Sets:
;   {table} help_requests
to process-help-request-message [message_type message_body sender_id]
    format-log 0 (word "Received HELP_REQUEST from " sender_id)
    table:put help_requests ticks (list sender_id message_body)
end
